pipeline {
    agent none
    stages {
        stage('Build and Test') {
            options {
                timeout(time: 10, unit: 'MINUTES')
            }
            matrix {
                axes {
                    axis {
                        name 'COMPILER'
                        values 'gcc', 'clang'
                    }
                }
                agent {
                    dockerfile {
                        filename 'ci/Dockerfile'
                        label 'linux && docker'
                    }
                }
                environment {
                    CC = "${env.COMPILER == 'gcc' ? 'gcc' : 'clang'}"
                    CXX = "${env.COMPILER == 'gcc' ? 'g++' : 'clang++'}"
                    HOME = "$WORKSPACE"
                    OMP_NUM_THREADS = "$PARALLEL"
                    UV_PYTHON_PREFERENCE = "only-system"
                }
                stages {
                    stage('Build') {
                        steps {
                            // Not using uv sync due to https://github.com/astral-sh/uv/issues/10996
                            sh '''
                                uv venv
                                . .venv/bin/activate
                                uv pip install -v -Csetup-args="-Dwerror=true" -Ccompile-args="-j$PARALLEL" .[test]
                            '''
                        }
                    }
                    stage('Test') {
                        steps {
                            sh '''
                                . .venv/bin/activate
                                pytest -vs
                            '''
                        }
                    }
                }
            }
        }

        stage('Build and Upload Wheels') {
            when {
                buildingTag()
                tag pattern: "v\\d+.*", comparator: "REGEXP"
            }
            options {
                timeout(time: 15, unit: 'MINUTES')
            }
            agent { label 'linux' }
            environment {
                HOME = "$WORKSPACE"
                OMP_NUM_THREADS = "$PARALLEL"
                CIBW_ENVIRONMENT_PASS_LINUX = "OMP_NUM_THREADS"
                UV_PYTHON_PREFERENCE = "system"
                PATH = "${env.HOME}/.local/bin${env.PATH ? ':' + env.PATH : ''}"
            }

            stages {
                stage('Install cibuildwheel') {
                    steps {
                        // We're using docker as a way to install uv from a cache
                        sh '''
                            id=$(docker create ghcr.io/astral-sh/uv:latest)
                            mkdir -p $HOME/.local/bin
                            docker cp $id:/uv $HOME/.local/bin/
                            docker rm $id
                            uv tool install cibuildwheel
                        '''
                    }
                }

                stage('Build') {
                    matrix {
                        axes {
                            axis {
                                name 'PYTHON'
                                // cp312 will build Limited API wheels for 3.12+
                                values 'cp39', 'cp310', 'cp311', 'cp312'
                            }
                        }
                        environment {
                            CIBW_BUILD = "${env.PYTHON}-*"
                        }

                        stages {
                            stage('Build') {
                                steps {
                                    sh 'cibuildwheel'
                                }
                            }
                        }
                    }
                }

                stage('Upload') {
                    environment {
                        UV_PUBLISH_TOKEN = credentials('pycorrfunc-testpypi')
                    }
                    steps {
                        sh 'uv publish --publish-url="https://test.pypi.org/legacy/" wheelhouse/*.whl'
                    }
                }
            }
        }
    }
}
